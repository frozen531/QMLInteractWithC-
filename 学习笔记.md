[TOC]

参考：[QML与Qt C++ 交互机制探讨与总结](https://www.cnblogs.com/aoldman/p/4103510.html?tdsourcetag=s_pctim_aiomsg)

# 1. QML与C++的结合使用

QML和 C++对象可以通过，signals,slots和 属性修改进行交互。
在这里不外乎有三种方法:
1. 把Qt C++中的对象或类型暴露(嵌入)给 QML端，供QML端使用。
2. QML中的Signal Handler(相当于Qt C++发送信号给QML端，QML端的Signal Handler进行处理)。
3. 在Qt C++端创建QML对象，既然对象都有了。那你想怎么样它就怎么样它呗。（没用过，看起来也不太实用，不过介绍介绍，有用过的同学留言哈）。
## 1.1 暴露Qt C++的对象或类型给QML
### 1.1.1注册Qt C++类类型给QML
一种方式是先将c++类注册成QML类型
##### main.cpp

```c++
#include <QGuiApplication>
#include <QQmlApplicationEngine>

#include "interactmembervariable.h"

int main(int argc, char *argv[])
{
    QCoreApplication::setAttribute(Qt::AA_EnableHighDpiScaling);

    QGuiApplication app(argc, argv);

    qmlRegisterType<InteractMemberVariable>("myTest.interactMemberVariable",1,0,"InteractMemberVariable"); // 1# 在qml中注册类

    QQmlApplicationEngine engine;
    engine.load(QUrl(QStringLiteral("qrc:/main.qml")));
    if (engine.rootObjects().isEmpty())
        return -1;

    return app.exec();
}
```
1. 在主函数中注册c++的类，包括版本号，在QML中使用的名字，类似Button这种，所以这里的最后一个参数**MyClassType**，首字母一定大写。

QML中这样使用
##### main.qml

```c++
import QtQuick 2.7
import QtQuick.Controls 2.0
import QtQuick.Layouts 1.0

import myTest.interactMemberVariable 1.0    // 1# 导入类对应的包，定义在main中的注册类处

ApplicationWindow {
    visible: true
    width: 640
    height: 480
    title: qsTr("暴露Qt C++的对象或类型给QML")

    Column{
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.topMargin: 10
        anchors.leftMargin: 10

        spacing: 10

        Label {                                // 使用 slots函数 将c++传入.text显示控件
            id: slotsLabel;
            height: 50
            color: "#FF0000"
            text: "slotsLabel"                  // 必须加上文本才会有大小，否则不占地方，排列位置有误
            font.pixelSize: 26;
        }

        Label {                                // 使用 Q_INVOKABLE函数 将c++传入.text显示控件
            id: inovkableLabel;
            height: 50
            color: "#FF0000"
            text: "inovkableLabel"
            font.pixelSize: 26;
        }

        Button{
            id:displayValue;
            text: "display_m_string";
            height: 50
            onClicked: {                    // 点击按钮发送数据到.text
                inovkableLabel.text = interaction.getMyStringByInvokable();    // 3.1# 使用 Q_INVOKABLE函数 函数名后面要加括号
                slotsLabel.text = interaction.myString;             // 3.2# 使用 slots函数 函数名后面没有括号
            }
        }

        Button{
            id:changeValueBySlots;
            text: "change_m_stringBySlots";
            height: 50
            onClicked: {                    // 3.1# 点击按钮 改变m_string的值
                interaction.setmyString("second")
            }
        }

        Button{
            id:changeValueByInvokable;
            text: "change_m_stringByInvokable";
            height: 50
            onClicked: {                    // 3.2# 点击按钮 改变m_string的值
                interaction.setMyStringByInvokable("third")
            }
        }
    }

    InteractMemberVariable {               // 2# 注册类
        id: interaction;
    }

}

```
1. 导入类对应的包名，包名我们在main.cpp中注册过了；
2. 创建类对应的对象，并赋予id，之后都是使用id调用类的一些方法；
3. 使用类中暴露出的函数，可以是**slots**或**Q_INVOKABLE**，这个在1.1.2中进行介绍，但是在.qml中的使用需要在这里强调一下：
> 1. **Q_INVOKABLE**声明的函数调用均需要加括号，它是不折不扣的函数；
> 2. slots中如果有**Q_PROPERTY**中的**READ**对应的函数，则不能加括号，作为属性使用；**WRITE**对应的函数因为要传参数，所以加括号，里面放入参数。
### 1.1.2 创建需要暴露给QML的数据类型
##### interactMemberVariable.h
```c++
/*
 * 暴露Qt C++的对象或类型给QML
*/

#ifndef InteractMemberVariable_H
#define InteractMemberVariable_H

#include <QObject>
#include <QDebug>

class InteractMemberVariable : public QObject
{
    Q_OBJECT

    Q_PROPERTY(QString myString READ myString WRITE setmyString NOTIFY myStringChanged) // 2# myString可与外交互的变量
public:
    explicit InteractMemberVariable(QObject *parent = nullptr);

    Q_INVOKABLE QString getMyStringByInvokable(); // 1.1# 可被qml直接调用的函数
    Q_INVOKABLE void setMyStringByInvokable(QString aString); // 1.1# 可被qml直接调用的函数

signals:
    void myStringChanged();             // 2# myString值改变的信号函数

public slots:  // 1.2# 可被qml直接调用的函数
    void setmyString(QString aString);  // 2# myString从qml传入 的槽函数
    QString myString();                 // 2# myString从c++传出 的槽函数

private:
    QString m_string = "first";
};

#endif // InteractMemberVariable_H

```
1. 若你想c++类数据元素中的==方法==可以被QML直接调用有2种方法：
> 1. 在函数申明前添加 Q_INVOKABLE 宏，定义在public中。
> 2. 申明成public slots.

2. QML可以直接访修改问改数据元素的==属性==，该属性由QPROPERTY所申明。

```c++
Q_PROPERTY(QString myString READ myString WRITE setmyString NOTIFY myStringChanged) // 2# myString可与外交互的变量

signals:
    void myStringChanged();             // 2# myString值改变的信号函数

public slots:  // 1.2# 可被qml直接调用的函数
    void setmyString(QString aString);  // 2# myString从qml传入 的槽函数
    QString myString();                 // 2# myString从c++传出 的槽函数

```

> 我们可以将**Q_PROPERTY**中提到的获取值QString **myString**()和设定值的函数void **setmyString**(QString aString)放在**public slots**中

##### interactMemberVariable.cpp

```c++
#include "InteractMemberVariable.h"

InteractMemberVariable::InteractMemberVariable(QObject *parent) : QObject(parent)
{

}

QString InteractMemberVariable::getMyStringByInvokable()
{
    qDebug() << "Q_INVOKABLE getMyString()" << m_string;
    return m_string;
}

void InteractMemberVariable::setMyStringByInvokable(QString aString)
{
    if(m_string!=aString)
    {
        m_string = aString;
        qDebug() << "Q_INVOKABLE setmyString(QString aString)" << m_string;
    }
}

void InteractMemberVariable::setmyString(QString aString)
{
    if(m_string!=aString)
    {
        m_string = aString;
        qDebug() << "slots setmyString(QString aString)" << m_string;
    }
}

QString InteractMemberVariable::myString()
{
    qDebug() << "slots myString()" << m_string;
    return m_string;
}

```

### 1.1.3 使用上下文设置，暴露已存在的Qt C++对象给QML

除了将c++类注册进QT体系内，还有就是在C++应用程序加载QML对象时，我们可以直接嵌入一些C++数据来给QML使用，这里需要用到QQmlContext::setContextProperty()，即设置QML上下问属性，它可以是一个简单的类型，也可以是任何我们自定义的类对象。

##### main.cpp

```c++
#include <QGuiApplication>
#include <QQmlApplicationEngine>

#include <QQuickView> // QQuickView
#include <QQmlContext>  // rootContext()

#include "interactmembervariable.h"

int main(int argc, char *argv[])
{
    QGuiApplication app(argc, argv);

    /************************************/
    QQuickView view;
    InteractMemberVariable interactMemberVariable;   // 提前建立类对象
    view.rootContext()->setContextProperty("interactMemberVariable", &interactMemberVariable); // 设置上下文属性
    view.setSource(QUrl(QStringLiteral("qrc:///main.qml")));
    view.show();
    /************************************/
    
    return app.exec();
}
```
这里使用了QQuickView，注意头文件的变化，InteractMemberVariable类先实例化为interactMemberVariable对象，然后注册为QML上下文属性。
##### main.qml
```c++
import QtQuick 2.7
import QtQuick.Controls 2.0
import QtQuick.Layouts 1.0

Item {
    visible: true
    width: 640
    height: 480
//    title: qsTr("暴露Qt C++的对象或类型给QML")   // Item没有title属性

    Column{
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.topMargin: 10
        anchors.leftMargin: 10

        spacing: 10

        Label {                                // 使用 slots函数 将c++传入.text显示控件
            id: slotsLabel;
            height: 50
            color: "#FF0000"
            text: "slotsLabel"                  // 必须加上文本才会有大小，否则不占地方，排列位置有误
            font.pixelSize: 26;
        }

        Label {                                // 使用 Q_INVOKABLE函数 将c++传入.text显示控件
            id: inovkableLabel;
            height: 50
            color: "#FF0000"
            text: "inovkableLabel"
            font.pixelSize: 26;
        }

        Button{
            id:displayValue;
            text: "display_m_string";
            height: 50
            onClicked: {                    // 点击按钮发送数据到.text
                inovkableLabel.text = interactMemberVariable.getMyStringByInvokable();    // 使用 Q_INVOKABLE函数 函数名后面要加括号  1#
                slotsLabel.text = interactMemberVariable.myString;             // 使用 slots函数 函数名后面没有括号  1#
            }
        }

        Button{
            id:changeValueBySlots;
            text: "change_m_stringBySlots";
            height: 50
            onClicked: {                    // 点击按钮 改变m_string的值
                interactMemberVariable.setmyString("second")                   // 1#
            }
        }

        Button{
            id:changeValueByInvokable;
            text: "change_m_stringByInvokable";
            height: 50
            onClicked: {                    // 点击按钮 改变m_string的值
                interactMemberVariable.setMyStringByInvokable("third")           // 1#
            }
        }
    }
}

```
1. 使用main.cpp中创建的类对象调用函数

### 1.2.2 设置上下文属性方式下的使用
信号函数在interactMemberVariable.cpp文件中不实现，使用时使用emit发送信号
##### interactMemberVariable.h

```c++
signals:
    void myStringChanged();             // 1# myString值改变的信号函数，值改变触发的信号text.text = "按下按钮，m_string值改变"
    void labelDisplay();              // 这个函数的功能是按钮显示m_string，触发text.text = "m_string值显示"
```
##### interactMemberVariable.cpp

```c++
void InteractMemberVariable::setMyStringByInvokable(QString aString)
{
    if(m_string!=aString)
    {
        m_string = aString;
        qDebug() << "Q_INVOKABLE setmyString(QString aString)" << m_string;
        emit myStringChanged();
    }
}

void InteractMemberVariable::setmyString(QString aString)
{
    if(m_string!=aString)
    {
        m_string = aString;
        qDebug() << "slots setmyString(QString aString)" << m_string;
        emit myStringChanged();
    }
}
```

##### main.qml

```javascript
import QtQuick 2.7
import QtQuick.Controls 2.0
import QtQuick.Layouts 1.0

Item {
    visible: true
    width: 640
    height: 480
//    title: qsTr("暴露Qt C++的对象或类型给QML")   // Item没有title属性

    Column{
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.topMargin: 10
        anchors.leftMargin: 10

        spacing: 10

        Label {                                // 使用 slots函数 将c++传入.text显示控件
            id: slotsLabel;
            height: 50
            color: "#FF0000"
            text: "slotsLabel"                  // 必须加上文本才会有大小，否则不占地方，排列位置有误
            font.pixelSize: 26;
        }

        Label {                                // 使用 Q_INVOKABLE函数 将c++传入.text显示控件
            id: inovkableLabel;
            height: 50
            color: "#FF0000"
            text: "inovkableLabel"
            font.pixelSize: 26;
        }

        Button{
            id:displayValue;
            text: "display_m_string";
            height: 50
            onClicked: {                    // 点击按钮发送数据到.text
                inovkableLabel.text = interactMemberVariable.getMyStringByInvokable();    // 使用 Q_INVOKABLE函数 函数名后面要加括号  1#
                slotsLabel.text = interactMemberVariable.myString;             // 使用 slots函数 函数名后面没有括号  1#
// 1#
interactMemberVariable.labelDisplay()
            }
        }

        Button{
            id:changeValueBySlots;
            text: "change_m_stringBySlots";
            height: 50
            onClicked: {                    // 点击按钮 改变m_string的值
                interactMemberVariable.setmyString("second")                   // 1#
            }
        }

        Button{
            id:changeValueByInvokable;
            text: "change_m_stringByInvokable";
            height: 50
            onClicked: {                    // 点击按钮 改变m_string的值
                interactMemberVariable.setMyStringByInvokable("third")           // 1#
            }
        }

        Rectangle{
                    id: signalLight
                    height: 50
                    width: 300
                    color: "green"

                    Text {
                        id: text
                        text: qsTr("just begin")
                        anchors.centerIn: signalLight
                        font.pixelSize: 26;
                    }
         }
    }

// 2# 设置上下文属性信号函数用法
Connections{
    target: interactMemberVariable
    onLabelDisplay: {
        text.text = "m_string值显示"
    }
    onMyStringChanged:{
        text.text = "按下按钮，m_string值改变"
    }
}
}

```
1. 调用信号函数
2. 信号函数具体实现哪些功能设定